RUN STAGE_INIT.
SET L_HEADING TO 90.
SET L_PITCH TO 90.
SET L_ROLL TO 270.
SET MECO TO FALSE.

LOCK STEERING TO HEADING(L_HEADING,L_PITCH) + R(0,0,L_ROLL).

RUN COUNTDOWN.

PRINT "IGNITION.".
LOCK THROTTLE TO 1.

WHEN SHIP:ALTITUDE > 45000 THEN {
  PRINT "DEPLOYMENT ALTITUDE".
  LOCK THROTTLE TO 0.
  //WAIT 1.
  AG10 ON.
  //WAIT 1.
}

UNTIL SHIP:PERIAPSIS > 75000 OR ABORT {
  CLEARSCREEN.
  PRINT "AP:" + SHIP:APOAPSIS.
  PRINT "PE:" + SHIP:PERIAPSIS.
  PRINT "TAP:" + ETA:APOAPSIS.
  PRINT "TPE:" + ETA:PERIAPSIS.
  PRINT "ALT:" + SHIP:ALTITUDE.
  PRINT "---".

  IF SHIP:ALTITUDE > 45000 AND ETA:PERIAPSIS < ETA:APOAPSIS {
    PRINT "DESCENT RECOVERY MANEUVER".
    SET WARP TO 0.
    LOCK STEERING TO SHIP:PROGRADE.
    LOCK THROTTLE TO 1.
  }
  ELSE IF SHIP:ALTITUDE > 70000 {
    PRINT "ORBITAL INSERTION".
    IF ETA:APOAPSIS <= 10 AND ETA:APOAPSIS >= 0 {
      SET WARP TO 0.
      LOCK THROTTLE TO 1.
    }
    ELSE IF ETA:APOAPSIS <= 20 AND ETA:APOAPSIS >= 10 {
      SET WARP TO 0.
      LOCK THROTTLE TO 1 - ((ETA:APOAPSIS - 10) / 10).
    }
    ELSE IF ETA:APOAPSIS <= 30 AND ETA:APOAPSIS >= 20 {
      SET WARP TO 0.
      LOCK THROTTLE TO 0.
    }
    ELSE {
      SET WARP TO 1.
      LOCK THROTTLE TO 0.
    }
    LOCK STEERING TO SHIP:PROGRADE.
  }
  ELSE IF SHIP:APOAPSIS > 80000 OR MECO {
    SET MECO TO TRUE.
    PRINT "MECO.".
    LOCK THROTTLE TO 0.
    LOCK STEERING TO SHIP:PROGRADE.
  }
  ELSE IF SHIP:APOAPSIS > 60000 {
    SET GUIDANCE TO (SHIP:APOAPSIS - 60000) / 20000.

    PRINT "PRE-INSERTION GUIDANCE".
    SET L_PITCH TO 0 - GUIDANCE * 10.
    LOCK THROTTLE TO 0.9.
  }
  ELSE IF SHIP:APOAPSIS > 30000 {
    SET GUIDANCE TO (SHIP:APOAPSIS - 30000) / 40000.

    PRINT "HIGH ASCENT".
    LOCK THROTTLE TO 1.
    SET L_PITCH TO 30 - GUIDANCE * 30.
  }
  ELSE IF SHIP:APOAPSIS > 5000 {
    SET GUIDANCE TO (SHIP:APOAPSIS - 5000) / 25000.

    PRINT "GRAVITY TURN".
    LOCK THROTTLE TO 1.
    SET L_PITCH TO 90 - GUIDANCE * 60.
  }
  ELSE IF SHIP:ALTITUDE > 500 {
    PRINT "BOOST PHASE".
    SET L_PITCH TO 90.

    IF SHIP:AIRSPEED > SHIP:TERMVELOCITY*1.1 {
      PRINT "HIGH DYNAMIC PRESSURE.".
      LOCK THROTTLE TO 0.66.
    }
    ELSE IF SHIP:AIRSPEED < SHIP:TERMVELOCITY*0.9 {
      PRINT "LOW TWR".
      LOCK THROTTLE TO 1.0.
    }
  }
  ELSE {
    PRINT "IGNITION.".
    LOCK THROTTLE TO 1.
    SET L_PITCH TO 90.
  }

  IF SHIP:ALTITUDE > 30000 AND SHIP:ALTITUDE < 40000 {
    PRINT "ASCENT ROLL MANEUVER".
    SET GUIDANCE TO (SHIP:ALTITUDE - 30000) / 10000.
    IF GUIDANCE > 1.0 {
      SET GUIDANCE TO 1.0.
    }

    SET L_ROLL TO 180 - GUIDANCE*180.
  }

  IF SHIP:ALTITUDE > 500 AND SHIP:ALTITUDE < 3000 {
    SET GUIDANCE TO (SHIP:ALTITUDE - 500) / 2000.
    IF GUIDANCE > 1.0 {
      SET GUIDANCE TO 1.0.
    }

    PRINT "BOOST PHASE ROLL MANEUVER".
    SET L_ROLL TO 270-GUIDANCE*90.
  }

  IF VANG(UP:VECTOR, SHIP:FACING:VECTOR) > (90 - L_PITCH + 5) {
    PRINT "COMPUTER INITIATED ABORT".
    PRINT "REASON: PITCH ANGLE NOT WITHIN TOLERANCE".
    ABORT ON.
  }

  IF SHIP:VERTICALSPEED < -2 AND ALT:RADAR < 30000 {
    PRINT "COMPUTER INITIATED ABORT".
    PRINT "REASON: UNPLANNED DESCENT".
    ABORT ON.
  }

  IF SHIP:VERTICALSPEED < -100 AND ALT:RADAR < 60000 {
    PRINT "COMPUTER INITIATED ABORT".
    PRINT "REASON: UNPLANNED REENTRY".
    ABORT ON.
  }

  RUN STAGE_IMMEDIATE.

  WAIT 1.0/20.0.
}

LOCK THROTTLE TO 0.

PRINT "COMPLETE".

IF ABORT {
  PRINT "ABORT DETECTED".
  PRINT "HANDING CONTROL TO REENTRY PROGRAM".
  RUN REENTER.
}