
IF ALT:RADAR < 10000 {
  PRINT "REENTRY COMPUTER DETECTS UNSAFE CONDITIONS".
  PRINT "SKIPPING COUNTDOWN".
}
ELSE {
  RUN COUNTDOWN.
}

IF SHIP:PERIAPSIS > 0 {
  PRINT "DEORBIT MANOUVER".
  LOCK STEERING TO SHIP:RETROGRADE.
  WAIT 10.
  LOCK THROTTLE TO 1.
  WAIT UNTIL SHIP:PERIAPSIS < 70000.
  LOCK THROTTLE TO 0.33.
  WAIT UNTIL SHIP:PERIAPSIS < 20000.
  PRINT "FINALIZING...".
  LOCK THROTTLE TO 0.1.
  WAIT UNTIL SHIP:PERIAPSIS < 0.
  LOCK THROTTLE TO 0.0.
  PRINT "DONE".
  WAIT 5.
}

IF ALT:RADAR > 70000 {
  PRINT "APPROACHING ATMOSPHERE BOUNDARY".
  UNTIL SHIP:ALTITUDE < 1000000 {
    SET WARP TO 6.
  }
  UNTIL SHIP:ALTITUDE < 250000 {
    SET WARP TO 5.
  }
  UNTIL SHIP:ALTITUDE < 70000 {
    SET WARP TO 3.
  }
  SET WARP TO 0.
  PRINT "DONE".

  PRINT "HIGH ALTITUDE STANDBY".
  PANELS OFF.
  WAIT UNTIL ALT:RADAR <= 45000.
}

IF ALT:RADAR > 5000 {
  PRINT "RELEASING SERVICE MODULE(S)".
  LOCK STEERING TO NORTH.
  WAIT UNTIL VANG(NORTH:VECTOR, SHIP:FACING:VECTOR) < 5.
  WAIT 5.
  AG9 ON.
  WAIT 5.
  PRINT "DONE".
}
ELSE {
  PRINT "LOW ALTITUDE".
  PRINT "EMERGENCY SERVICE MODULE SEPARATION".
  RCS ON.
  LOCK STEERING TO NORTH.
  WAIT 0.5.
  AG9 ON.
  PRINT "DONE".
  WAIT 2.
}

IF ALT:RADAR > 30000 {
  PRINT "DESCENT ATTITUDE CONTROL".
  LOCK STEERING TO SHIP:RETROGRADE.
  WAIT 15.
  PRINT "DONE".

  PRINT "SPIN STABILIZATION MANEUVER".
  SAS ON.
  UNLOCK STEERING.
  WAIT 2.
  SAS OFF.
  SET SHIP:CONTROL:ROLL TO 0.05.
  WAIT 4.
  SET SHIP:CONTROL:ROLL TO 0.
  PRINT "DONE".
}
ELSE {
  PRINT "EMERGENCY ATTITUDE CONTROL".
  LOCK STEERING TO SHIP:SRFRETROGRADE.
  PRINT "DONE".
}

WAIT UNTIL ALT:RADAR < 2500.
PRINT "VERTICAL LANDING STABILIZATION".
LOCK STEERING TO SHIP:SRFRETROGRADE:VECTOR + UP:VECTOR * 2.25.

WAIT UNTIL ALT:RADAR < 250 AND SHIP:VERTICALSPEED < -5.

LEGS ON.
CHUTES ON.
AG8 ON.

PRINT "FINAL DESCENT".
LOCK ALT_ADJUST TO MAX( 0, ALT:RADAR/5 - 10 ).
LOCK THROTTLE TO MAX(0,
    MIN(1,
        (-SHIP:VERTICALSPEED - 2 - ALT_ADJUST)/(5 + ALT_ADJUST))).

UNTIL FALSE {
  CLEARSCREEN.
  PRINT "RADAR:" + ALT:RADAR.
  PRINT "SPEED:" + SHIP:VERTICALSPEED.
  WAIT 0.2.
}